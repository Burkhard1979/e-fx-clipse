grammar at.bestsolution.efxclipse.tooling.fxgraph.FXGraph with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate fXGraph "http://www.bestsolution.at/efxclipse/tooling/fxgraph/FXGraph"

Model:
	imports+=Import*
	rootNode=Element;
	
Import:
	'import' importedNamespace=QualifiedNameWithWildCard;

Element:
	type=JvmTypeReference ('(' (value = SimpleValueProperty | 'createdby' factory=STRING) ')')? ('id' name=ValidID)? ('{'
		('|' defines+=Define (',' defines+=Define)* '|')?
		properties+=Property (',' properties+=Property)*
	'}')?
;

Define:
	element=Element
;

Property:
	name=ID ':' value=ValueProperty 
;

ValueProperty:
	SingleValueProperty | MultiValueProperty
;

SingleValueProperty:
	SimpleValueProperty | Element | ReferenceValueProperty | IncludeValueProperty
;

MultiValueProperty:
	ListValueProperty | MapValueProperty
;

ListValueElement:
	Element | ReferenceValueProperty | IncludeValueProperty
;

ListValueProperty:
	'[' value += ListValueElement (',' value += ListValueElement)* ']'
;

MapValueProperty:
	'{' properties+=Property (',' properties+=Property)* '}'
;

SimpleValueProperty:
	stringValue=STRING | booleanValue=("true"|"false") | ( (negative?='-')? (intValue=INT | realValue=REAL))
;

ReferenceValueProperty:
	'idref' reference=[Element|ValidID]
;

IncludeValueProperty:
	'include' source=STRING
;

QualifiedNameWithWildCard :
	QualifiedName  ('.' '*')?;

terminal REAL returns ecore::EDouble: (('0'..'9')*"."('0'..'9')+);