grammar at.bestsolution.efxclipse.tooling.fxmlx.FXMLDsl hidden(ML_COMMENT,WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate fXMLDsl "http://www.bestsolution.at/efxclipse/tooling/fxmlx/FXMLDsl"

FXML:
	xmlDec=XMLDec
	processingInstructions+=ProcessingInstruction*
	rootElement=ElementDefinition;

XMLDec:
	'<?xml' props+=AttributePropertyDefinition+ '?>'
;

ProcessingInstruction:
	'<?' type=ID importedNamespace=QualifiedNameWithWildCard '?>'
;

ElementDefinition:
	ContainerElementDefinition | EmptyElementDefinition
;

ContainerElementDefinition:
	('<' (namespace=ID ':')? name=QualifiedName properties+=AttributePropertyDefinition*'>'
		children+=ElementDefinition+
	 '</' (endnamespace=ID ':')? endname=QualifiedName '>'
	)
	| 
	('<' (namespace=ID ':')? name=QualifiedName properties+=AttributePropertyDefinition*'>'
		content=Content
	 '</' (endnamespace=ID ':')? endname=QualifiedName '>'
	)
;

EmptyElementDefinition:
	('<' (namespace=ID ':')? name=QualifiedName props+=AttributePropertyDefinition* '/>') 
;



AttributePropertyDefinition:
	(namespace=ID ':')? name=ID '=' value=STRING
;


Content hidden(ML_COMMENT):
	(ID|STRING|ANY_OTHER|WS|'.')*
;

QualifiedName:
  ID (=>'.' ID)*
;


QualifiedNameWithWildCard :
	QualifiedName ('.' '*')?
;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		;
terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
terminal ML_COMMENT	: '<!--' -> '-->';